function determine_direction with pos, dir do
  set left_boundary to 1
  set right_boundary to 11

  if pos >= right_boundary do
    return "left"
  else if pos <= left_boundary do
    return "right"
  end
  return dir
end

function move with pos, dir do
  if dir == "right" do
    move_right()
    change pos to pos + 1
  else if dir == "left" do
    move_left()
    change pos to pos - 1
  end
  return pos
end

function all_aliens_dead with aliens do
  for each alien in aliens do
    if alien do
      return false
    end
  end

  return true
end

// Get the first row of aliens (the bottom one)
set bottom_row to get_starting_aliens_in_row(1)
set middle_row to get_starting_aliens_in_row(2)
set top_row to get_starting_aliens_in_row(3)

// Set variables to track things
set direction to "right"
set position to 1
set shot to false

// Play the game
repeat do
  change shot to false
  for each row in [bottom_row, middle_row, top_row] do
    if shot == false and row[position] do
      shoot()
      change row[position] to false
      change shot to true
    end
  end

  if all_aliens_dead(bottom_row) and all_aliens_dead(middle_row) and all_aliens_dead(top_row) do
    fire_fireworks()
  else do
    change direction to determine_direction(position, direction)
    change position to move(position, direction)
  end
end